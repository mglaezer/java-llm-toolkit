import gg.jte.ContentType

//file:noinspection SpellCheckingInspection
plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'gg.jte.gradle' version '3.2.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

// enable parameter names retention
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-parameters']
}

spotless {
    java {
        palantirJavaFormat('2.61.0')
    }
}



sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated/sources/jte']
        }
    }
}

jte {
    sourceDirectory = file('src/main/resources/templates').toPath()
    targetDirectory = file('build/generated/sources/jte').toPath()
    contentType = ContentType.Plain
}

spotlessJava {
    dependsOn 'generateJte'
}

compileJava {
    dependsOn 'generateJte'
    dependsOn 'spotlessApply'
}


repositories {
    mavenCentral()
}

ext {
    langchain4jVersion = '1.0.0-beta3'
    lombokVersion = '1.18.38'
    junitVersion = '5.9.2'
}


dependencies {
    implementation 'net.bytebuddy:byte-buddy:1.17.5'
    implementation 'com.google.guava:guava:33.4.6-jre'
    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.3'

    implementation "dev.langchain4j:langchain4j:${langchain4jVersion}"
    implementation "dev.langchain4j:langchain4j-open-ai:${langchain4jVersion}"
    implementation "dev.langchain4j:langchain4j-anthropic:${langchain4jVersion}"
    implementation "dev.langchain4j:langchain4j-google-ai-gemini:${langchain4jVersion}"

    implementation 'org.springframework:spring-web:6.2.5'
    implementation 'gg.jte:jte:3.2.0'

    implementation 'io.github.cdimascio:dotenv-java:3.2.0'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.12.1'

    implementation 'ch.qos.logback:logback-classic:1.5.18'
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('javadoc') {
    options.addStringOption('Xdoclint:none', '-quiet')
}


publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        maven(MavenPublication) {
		//noinspection GroovyAssignabilityCheck
            from components.java
        }
    }
}